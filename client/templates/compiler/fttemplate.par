[*
	var fttemplate = function() {
		var result;
*]


/~
	--- These are the token definitions ---
~/

/~ Characters to be ignored... ~/
!	'//[^\n]*\n'
	;

/~	Grammar Tokens			~/
	'(<p:textnode>([^<^>]*)</p:textnode>)'						TEXTNODE   [* %match = %match.substr(12, %match.length - 25); *] 	
	'(<p:function>(([^<]|<[^/])*)</p:function>)'				JSFUN	   [* %match = %match.substr(12, %match.length - 25); *] 	
	'template([\r\n\t\ ]*)'										WTEMPLATE
	'action([\r\n\t\ ]*)'										WACTION
	'state([\r\n\t\ ]*)'										WSTATE
	'create([\r\n\t\ ]*)'										WCREATE
	'add([\r\n\t\ ]*)'											WADD
	'extract([\r\n\t\ ]*)'										WEXTRACT
	'remove([\r\n\t\ ]*)'										WREMOVE
	'style([\r\n\t\ ]*)'										WSTYLE
	'as([\r\n\t\ ]*)'											WAS
	'if([\r\n\t\ ]*)'											WIF
	'else([\r\n\t\ ]*)'											WELSE
	'f:each([\r\n\t\ ]*)'										FEACH
	'f:call([\r\n\t\ ]*)'										FCALL
	'f:on([\r\n\t\ ]*)'											FON
	'f:trigger([\r\n\t\ ]*)'									FTRIGGER
	'\{([\r\n\t\ ]*)'											LBRACKET
	'\}([\r\n\t\ ]*)'											RBRACKET
	'\(([\r\n\t\ ]*)'											LPAREN
	'\)([\r\n\t\ ]*)'											RPAREN
	',([\r\n\t\ ]*)'											COMMA
	';([\r\n\t\ ]*)'											SEMICOLON
	':([\r\n\t\ ]*)'											COLON
	'=([\r\n\t\ ]*)'											EQUALS
	'</([\r\n\t\ ]*)'											LTSLASH
	'/([\r\n\t\ ]*)'											SLASH
	'<([\r\n\t\ ]*)'											LT
	'>([\r\n\t\ ]*)'											GT
	'-([\r\n\t\ ]*)'											DASH
	'\"([\r\n\t\ ]*)'											QUOTE
	'[A-Za-z0-9_\.\'\~\#\*\+\^\%\.\!\&\|\?\[\]]+([\r\n\t\ ]*)'	IDENTIFIER
	;
##

/~
	--- And here's the grammar specification ---
~/

TOP:
	LINE												[* result = {line: %1}; *]
	;


LINE:
	JSFUN
	| TEMPLATE
	| STATE
	| LETLISTBLOCK
	| IFBLOCK
	| ACTIONTPL
	| EXPR
	| XML
	;


TEMPLATE:
	WTEMPLATE LPAREN ARGLIST RPAREN LBRACKET FULLLETLIST RBRACKET
	;

ARGLIST:
	ARGLIST COMMA VARIABLE								
	| VARIABLE											
	|													
	;

VARIABLE:
	IDENTIFIER											
	| IDENTIFIER COLON COLON TYPE						
	;

FULLLETLIST:
	LETLIST LINE										
	| LETLIST LINE COMMA								
	;

LETLISTBLOCK:
	LBRACKET FULLLETLIST RBRACKET
	;

LETLIST:
	LETLIST LET COMMA									
	|													
	;

LET:
	VARIABLE EQUALS LINE								
	;

STATE:
	WSTATE LPAREN TYPE RPAREN							
	| WSTATE LBRACKET FULLACTLIST RBRACKET				
	;

TYPE:
	TYPE TYPE
	| IDENTIFIER
	| DASH GT
	;

IFBLOCK:
	WIF EXPR WAS ASKEYVAL LBRACKET FULLLETLIST RBRACKET WELSE IFBLOCK
	| WIF EXPR WAS ASKEYVAL LBRACKET FULLLETLIST RBRACKET WELSE LBRACKET FULLLETLIST RBRACKET
	;

ACTIONTPL:
	WACTION LPAREN ARGLIST RPAREN LBRACKET FULLACTLIST RBRACKET
	;

FULLACTLIST:
	ACTLIST ACTION
	| ACTLIST
	;

ACTLIST:
	ACTLIST ACTLINE COMMA
	|
	;

ACTLINE:
	VARIABLE EQUALS ACTION
	| ACTION
	;

ACTION:
	CREATE
	| UPDATE
	| EXTRACT
	| JSFUN
	| TEMPLATE
	| ACTIONTPL
	| EXPR
	| STATE
	| LETLISTBLOCK
	| XML
	;

CREATE:
	WCREATE LPAREN TYPE COMMA LBRACKET PROPLIST RBRACKET RPAREN
	| WCREATE LPAREN TYPE RPAREN
	;

PROPLIST:
	PROPLIST COMMA PROP
	| PROP
	|
	;

PROP:
	IDENTIFIER COLON EXPR
	;


UPDATE:
	ADD
	| REMOVE
	;

ADD:
	WADD LPAREN EXPR COMMA EXPR RPAREN
	| WADD LPAREN EXPR COMMA EXPR COMMA EXPR RPAREN
	;

REMOVE:
	WREMOVE LPAREN EXPR COMMA EXPR RPAREN
	| WREMOVE LPAREN EXPR RPAREN
	;

EXTRACT:
	WEXTRACT EXPR WAS ASKEYVAL LBRACKET FULLACTLIST RBRACKET
	| VARIABLE EQUALS WEXTRACT EXPR
	;

EXPR:
	IDENTIFIER 
	| STRINGESCAPEQUOTES 
	| LPAREN EXPR RPAREN
	| IDENTIFIER COLON COLON IDENTIFIER
	| IDENTIFIER COLON IDENTIFIER
	| DASH GT
	| DASH IDENTIFIER
	| EXPR EXPR
	;

XML:
	FOREACH												
	| TRIGGER											
	| ON												
	| CALL												
	| TAG												
	| TEXTNODE												
	;

FOREACH:
	LT FEACH EXPR WAS ASKEYVAL GT FULLLETLIST LTSLASH FEACH GT	
	| LT FEACH EXPR GT FULLLETLIST LTSLASH FEACH GT				
	;

TRIGGER:
	LT FTRIGGER EXPR WAS ASKEYVAL GT FULLACTLIST LTSLASH FTRIGGER GT	
	| LT FTRIGGER EXPR GT FULLACTLIST LTSLASH FTRIGGER GT				
	;

ON:
	LT FON IDENTIFIER GT FULLACTLIST LTSLASH FON GT			
	;

CALL:
	LT FCALL GT FULLLETLIST LTSLASH FCALL GT				
	;

TAG:
	LT TAGNAME ATTRIBUTES GT XMLLIST LTSLASH TAGNAME GT		
	| LT TAGNAME ATTRIBUTES SLASH GT						
	;

TAGNAME:
	IDENTIFIER
	| IDENTIFIER COLON IDENTIFIER							
	;


ASKEYVAL:
	IDENTIFIER											
	| IDENTIFIER COMMA IDENTIFIER						
	;

XMLLIST:
	XMLLIST XML											
	| 													
	;

ATTRIBUTES:
	ATTRIBUTES ATTASSIGN								
	|													
	;

ATTASSIGN:
	WSTYLE EQUALS QUOTE STYLELIST QUOTE					
	| ATTNAME EQUALS ATTRIBUTE							
	;

ATTNAME:
	IDENTIFIER
	| KEYWORD
	| ATTNAME DASH IDENTIFIER							
	;

ATTRIBUTE:
	STRING												
	| QUOTE INSERT QUOTE								
	;

INSERT:
	LBRACKET EXPR RBRACKET								
	;

STYLELIST:
	STYLELIST SEMICOLON STYLEASSIGN
	| STYLEASSIGN
	| STYLELIST SEMICOLON
	|
	;

STYLEASSIGN:
	ATTNAME COLON STYLETEXT
	| ATTNAME COLON INSERT
	;

STYLETEXT:
	KEYWORD
	| IDENTIFIER
	| COMMA
	| LPAREN
	| RPAREN
	| EQUALS
	| STYLETEXT DASH STYLETEXT							
	| STYLETEXT STYLETEXT								
	;

TEXT:
	KEYWORD
	| LBRACKET
	| RBRACKET
	| LPAREN
	| RPAREN
	| COMMA
	| SEMICOLON
	| COLON
	| EQUALS
	| LTSLASH
	| SLASH
	| LT
	| GT
	| IDENTIFIER
	| TEXT DASH TEXT
	| TEXT TEXT
	|
	;

KEYWORD:
	TEXTNODE
	| WTEMPLATE
	| WACTION
	| WSTATE
	| WCREATE
	| WADD
	| WEXTRACT
	| WREMOVE
	| WSTYLE
	| WAS
	| WIF
	| WELSE
	| FEACH
	| FCALL
	| FON
	| FTRIGGER
	;

STRINGESCAPEQUOTES:
	QUOTE TEXT QUOTE
	;

STRING:
	QUOTE TEXT QUOTE
	;
		
	
[*
		return {
			parse:function(arg1, arg2, arg3) {
				var errcount = __parse(arg1, arg2, arg3);
				if (errcount == 0) {
					return {
						success:true,
						result:result
					};
				} else {
					return {
						success:false,
						result:errcount
					};
				}
			}
		};
	}();
*]
